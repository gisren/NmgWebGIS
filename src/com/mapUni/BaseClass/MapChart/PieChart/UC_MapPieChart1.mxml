<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 creationComplete="init()"
		 >
	<fx:Script>
		<![CDATA[
			import mx.charts.ChartItem;
			import mx.charts.HitData;
			import mx.charts.series.PieSeries;
			import mx.charts.series.items.PieSeriesItem;
			import mx.collections.ArrayCollection;
			import mx.graphics.IFill;
			import mx.graphics.SolidColor;
			
			[Bindable]
			public var V_ChartData:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			public var V_DataField:String = "";
			
			[Bindable]
			public var V_seriesNameField:String = "";
			
			[Bindable]
			public var V_LabelPosition:String = "inside";
			
			[Bindable]
			public var V_seriesDataField:String = "";
			
			public var V_dataField:String = "";
			public var V_nameField:String = "";
			public var V_colorField:String = "";
			public var V_toolTipField:String = "";
			
			
			
			private function init():void
			{
				resetData();
				
				createSeriesList();
			}
			
			
			
			/**
			 * 整理数据
			 */
			private function resetData():void
			{
				
				for(var i:int=0; i<V_ChartData.length; i++)
				{
					var itemObj:Object = V_ChartData[i];
					
					var seriesData:Array = itemObj[V_seriesDataField];
					
					for(var n:int=0; n<seriesData.length; n++)
					{
						var seriesObj:Object = seriesData[n];
						
						var data:String = seriesObj[V_dataField];
						itemObj["data" + n.toString()] = data;
						
						var color:String = seriesObj[V_colorField];
						itemObj["color" + n.toString()] = color;
						
						var name:String = seriesObj[V_nameField];
						itemObj["name" + n.toString()] = name;
						
					}
				}
			}
			
			
			/**
			 * 组织显示序列
			 */
			private function createSeriesList():void
			{
				var seriesArray:Array = new Array();
				
				/* 
				根据第一组的序列数添加序列
				*/
				var seriesData:Array = V_ChartData[0][V_seriesDataField];
				
				
				for(var n:int=0; n<seriesData.length; n++)
				{
					var seriesObj:Object = seriesData[n];
					
					var series:PieSeries = new PieSeries();
					
					series.id = n.toString();
					
					//数据字段
					series.field = "data" + n.toString();
					
					//饼图区域颜色
					series.fillFunction = colorFunction;
					
					/* 
					设置饼图的显示标签位置和内容
					如果不设置"labelPosition"属性，则不会执行"labelFunction"的方法
					*/
//					series.setStyle("labelPosition","inside");
//					series.labelFunction = displayGold;
					
					//鼠标移动到图标上时鼠标旁边的提示信息
//					series.toolTip = seriesObj[V_toolTipField];
					
					seriesArray.push(series);
				} 
				
				piechart.series = seriesArray;
			}
			
			
			/**
			 * 饼图的各区域颜色  
			 */
			private function colorFunction(element:ChartItem, index:Number):IFill
			{
				var pieSeries:PieSeries = element.element as PieSeries;
				
				var color:Number = element.item["color" + pieSeries.id.toString()];
				
				return new SolidColor(color, 1);
			}
			
			
			/**
			 * 饼图上标签 
			 */
			private function displayGold(data:Object, field:String, index:Number, percentValue:Number):String 
			{
				var temp:String= (" " + percentValue).substr(0,6);
				return data[V_seriesNameField] + '\n'  	//区域名
					+ data[V_nameField + field.substr(-1,1)] + '\n' 	//类别名
					+ data[field] + '\n' 		//数据名
					+ temp + "%";				//百分比指数
			}
			
			
			/**
			 * 饼图的数据提示 
			*/
			private function dataTipFunc(hitData:HitData):String
			{
				var pieSeriesItem:PieSeriesItem = hitData.chartItem as PieSeriesItem;
				var pieSeries:PieSeries = pieSeriesItem.element as PieSeries;
				
				var name:String = pieSeriesItem.item[V_seriesNameField];
				
				var calss:String = pieSeriesItem.item["name" + pieSeries.id.toString()];
				
				var value:String = pieSeriesItem.value.toString();
				
				var percentValue:String = pieSeriesItem.percentValue.toFixed(2).toString() + "%";
				
				
				return calss + '\n' + name + '\n' + percentValue + '\n(' + value + ')';
			}
			
			
			
		]]>
	</fx:Script>
	
		
	<mx:PieChart id="piechart" height="100%" width="100%" color="0x323232" 
				 showDataTips="true" dataProvider="{V_ChartData}" dataTipFunction="dataTipFunc" >
		
		<!--<mx:series>
			<mx:PieSeries id="pieSeries" labelPosition="{V_LabelPosition}"  field="{V_DataField}" labelFunction="displayGold" />
		</mx:series>-->
	</mx:PieChart>
		
</s:Group>
