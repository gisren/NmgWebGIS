<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
// Copyright (c) 2012 中科宇图天下科技有限公司 - 环保事业部
//
// 周边查询
// 
// 绘制图形查询指定图形信息
// 
// Author：liuXL
// Date：2012.08.23
//
////////////////////////////////////////////////////////////////////////////////
-->
<viewer:BaseWidget xmlns:esri="http://www.esri.com/2008/ags"
				   xmlns:fx="http://ns.adobe.com/mxml/2009"
				   xmlns:s="library://ns.adobe.com/flex/spark"
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   xmlns:viewer="com.esri.viewer.*"
				   xmlns:Search="widgets.toolBar.Search.*"
				   widgetConfigLoaded="init()" 
				   xmlns:DataGrid="com.mapUni.UtilComponent.DataGrid.*" 
				   xmlns:pageNavigation="com.mapUni.UtilComponent.pageNavigation.*" xmlns:Buffer="widgets.toolBar.Buffer.*" 
				   >
	<fx:Script>
		<![CDATA[
			import com.esri.ags.FeatureSet;
			import com.esri.ags.Graphic;
			import com.esri.ags.SpatialReference;
			import com.esri.ags.components.supportClasses.InfoWindow;
			import com.esri.ags.events.DrawEvent;
			import com.esri.ags.events.GeometryServiceEvent;
			import com.esri.ags.events.LayerEvent;
			import com.esri.ags.geometry.Geometry;
			import com.esri.ags.geometry.MapPoint;
			import com.esri.ags.geometry.Polygon;
			import com.esri.ags.geometry.Polyline;
			import com.esri.ags.layers.FeatureLayer;
			import com.esri.ags.layers.GraphicsLayer;
			import com.esri.ags.symbols.CompositeSymbol;
			import com.esri.ags.symbols.FillSymbol;
			import com.esri.ags.symbols.LineSymbol;
			import com.esri.ags.symbols.PictureMarkerSymbol;
			import com.esri.ags.symbols.SimpleFillSymbol;
			import com.esri.ags.symbols.SimpleLineSymbol;
			import com.esri.ags.symbols.SimpleMarkerSymbol;
			import com.esri.ags.symbols.Symbol;
			import com.esri.ags.symbols.TextSymbol;
			import com.esri.ags.tasks.GeometryService;
			import com.esri.ags.tasks.supportClasses.Query;
			import com.esri.ags.tools.DrawTool;
			import com.esri.ags.webmap.PopUpRenderer;
			import com.esri.viewer.AppEvent;
			import com.esri.viewer.ViewerContainer;
			import com.mapUni.BaseClass.InfoWindow.InfoResult;
			import com.mapUni.BaseClass.MapUni;
			import com.mapUni.UtilComponent.pageNavigation.UE_PageNavigationEvent;
			
			import flash.utils.setTimeout;
			
			import flashx.textLayout.edit.TextScrap;
			
			import mx.collections.ArrayCollection;
			import mx.events.CollectionEvent;
			import mx.managers.CursorManager;
			import mx.rpc.AsyncResponder;
			import mx.utils.StringUtil;
			
			import org.osmf.layout.AbsoluteLayoutFacet;
			
			import widgets.toolBar.Search.SearchResult;
			
			
			
			[Bindable]
			/** 是否显示搜索结果信息 */
			private var msgVisible:Boolean = false;
			
			[Bindable]
			/** 搜索结果 */
			private var searchResultAC:ArrayCollection = new ArrayCollection();
			
			/** 配置后查询图层 */
			private var configSearchText:Array = new Array();
			
			/** 已经查询完的图层个数  */
			private var searchingNum:Number = 0;
			
			/** 是否已经查询完毕 */
			private var isSearchEnd:Boolean = true;
			
			/** 绘制图层 */
			private var graphicLayer:GraphicsLayer = new GraphicsLayer();
			
			/** 标识图层 */
			private var labelGraLayer:GraphicsLayer = new GraphicsLayer();
			
			/** 缓冲图形 */
			private var baseGraLayer:GraphicsLayer = new GraphicsLayer();
			
			/** 当前选中图形 */
			private var currGraphic:Graphic = new Graphic();
			
			
			private function init():void
			{
				map.addLayer(baseGraLayer);
				map.addLayer(graphicLayer);
				map.addLayer(labelGraLayer);
				
				
				if(configXML)
				{
					arrangeLayers();
				}
				
				ViewerContainer.addEventListener(AppEvent.CLEAR_MAP, onClearMap);
				
				//				map.infoWindow.addEventListener(Event.CLOSE, onInfoWindowclese);
			}
			
			
			/**
			 * 整理搜索图层 
			 */
			private function arrangeLayers():void
			{
				var lyrList:XMLList = configXML.layers.layer;
				
				for (var i:int = 0; i < lyrList.length(); i++)
				{
					var lyrLabel:String = lyrList[i].name;
					var lyrExpr:String = lyrList[i].expression;
					var lyrFields:XMLList = lyrList[i].fields;
					var lyrTitleField:String = lyrList[i].titlefield;
					var useAMF:String = lyrList[i].useAMF;
					
					var featureLayUrl:String = MapUni.layerUrl(map, lyrLabel);
					
					if(featureLayUrl)
					{
						var layer:FeatureLayer = new FeatureLayer(featureLayUrl);
						if (useAMF)
						{
							layer.useAMF = useAMF == "true";
						}
						if (lyrFields && lyrFields[0].@all[0] == "true")
						{
							layer.outFields = [ "*" ];
						}
						else if (lyrFields)
						{
							var fields:XMLList = lyrFields.field;
							layer.outFields = [];
							for each (var fieldXML:XML in fields)
							{
								if (fieldXML.@name[0])
								{
									layer.outFields.push(fieldXML.@name[0]);
								}
							}
						}
						
						var searchLayer:Object =
							{
								layer: layer,
								label: lyrLabel,
								expr: lyrExpr,
								fields: lyrFields,
								titlefield: lyrTitleField
							};
						
						if (lyrExpr)
						{
							configSearchText.push(searchLayer);
						}
					}
					else
					{
						showErrorInfo(lyrLabel + " 图层在地图中没有找到对应的图层");
					}
				}
			}
			
			
			/**
			 * 功能: 激活绘图工具 
			*/
			private function activateSearchTool(value:String):void
			{
				clear();
				var status:String;
				var drawSymbol:Symbol;
				switch (value)
				{
					
					case DrawTool.MAPPOINT:
					{
						status = "点查询";
						drawSymbol = new SimpleMarkerSymbol(SimpleMarkerSymbol.STYLE_CIRCLE, 15, 0xFF0000, 2);
						break;
					}
						
					case DrawTool.POLYLINE:
					{
						status = "线查询";
						drawSymbol = new SimpleLineSymbol(SimpleLineSymbol.STYLE_SOLID, 0x0000ff, 1, 2);
						break;
					}
						
					case DrawTool.POLYGON:
					{
						status = "多边形查询";
						drawSymbol = new SimpleFillSymbol(SimpleFillSymbol.STYLE_SOLID, 0x3FAFDC, 0.5, new SimpleLineSymbol(SimpleLineSymbol.STYLE_SOLID, 0x3FAFDC, 1, 1));
						break;
					}
				}
				setMapAction(value, status, drawSymbol, searchDrawEnd);;		
			}			
			
			
			/**
			 * 监听: 绘图完毕 
			*/
			private function searchDrawEnd(event:DrawEvent):void
			{
				event.target.deactivate();
				var geom:Geometry = event.graphic.geometry;
				
				baseGraLayer.add(event.graphic);
				buffer(geom);
			}
			
			
			/**
			 * 功能: 缓冲绘制图形 
			*/
			public function buffer(geom:Geometry):void
			{
				showMessage("正在缓冲周边...", true);
				
				var geomServerUrl:String = configData.webService.geometryService;
				var dist:Number = new Number(bufferRadiusInput.value);
				var distUnit:Number = GeometryService.UNIT_METER;
				MapUni.buffer(geomServerUrl, [geom], [dist], distUnit, bufferCompleteHandler, null, null, null, map.spatialReference);
				
				function bufferCompleteHandler(GeomArray:Array):void
				{
					showMessage("缓冲完成", false);
					
					for each (var geoPolygon:Polygon in GeomArray)
					{
						var graphic:Graphic = new Graphic();
						graphic.geometry = geoPolygon;
						graphic.symbol = new SimpleFillSymbol("solid", 0x69C471, 0.5, new SimpleLineSymbol());
						baseGraLayer.add(graphic);
						
						queryFeaturesGraphical(graphic.geometry);
						
//						MapUni.dataExtent([geoPolygon], map);
					}
				}
			}
			
			
			/**
			 * 图层图形查询
			 * 检查图层加载状态  
			 */
			private function queryFeaturesGraphical(geom:Geometry):void
			{
				if(!isSearchEnd)
				{
					showMessage("上次查询还未完成，请等待完成后再进行查询", false);
					return;
				}
				
				var unLoadNum:uint = 0;
				
				/*
				验证图层是否已经全部加载完成
				*/
				for(var i:int=0; i<configSearchText.length; i++)
				{
					var queryLayer:FeatureLayer = configSearchText[i].layer;
					
					if (queryLayer && !queryLayer.loaded)
					{
						queryLayer.addEventListener(LayerEvent.LOAD, queryLayer_loadHandler);
						
						function queryLayer_loadHandler(event:LayerEvent):void
						{
							queryFeaturesGraphical(geom);
						}
						//						return;
						
						unLoadNum++;
					}
				}
				
				/*  lxl 注 用于显示未能成功加载的图层个数
				if(unLoadNum)
				{
				showErrorInfo("错误：未加载完成的图层个数" + unLoadNum.toString());
				} */
				
				resetData();
				
				for(var n:int=0; n<configSearchText.length; n++)
				{
					var searchParam:Object = configSearchText[n];
					
					searchLayer(searchParam, geom);
				}
				
			}
			
			
			/**
			 * 准备查询前的参数设置
			 */
			private function resetData():void
			{
				//已查询过的图层数归零
				searchingNum = 0;
				
				//是否查询完毕改为否
				isSearchEnd = false;
				
				//显示忙碌图标
				CursorManager.setBusyCursor();
				
				//查询结果清空
				searchResultAC.removeAll();
				pageNavi.pageData = searchResultAC;
				
				//清空图层
				//clear();
				
				//结果列表清空
				searchResultItem.dataProvider = null;
				
				//显示搜索状态图标
				showMessage("正在搜索周边…", true);
			}
			
			
			/**
			 * 查询单个图层
			 */
			private function searchLayer(configSearch:Object, geom:Geometry):void
			{
				var queryLayer:FeatureLayer = configSearch.layer;
				
				if (queryLayer && geom)
				{
					var query:Query = new Query();
					query.geometry = geom;
					query.outSpatialReference = map.spatialReference;
					queryLayer.queryFeatures(query, new AsyncResponder(onResult, onFault, configSearch));
					
					function onResult(featureSet:FeatureSet, token:Object = null):void
					{
						searchingNum++;
						
						createSearchResults(featureSet, token);
						
						if(searchingNum == configSearchText.length)
						{
							onSearchEnd();
						}
					}
					
					function onFault(info:Object, token:Object = null):void
					{
						searchingNum++;
						if(searchingNum == configSearchText.length)
						{
							onSearchEnd();
						}
						
						showErrorInfo(
							"错误：在检索图层信息时出错\n"+
							"图层名称：" + token.label + "\n" +
							"检索条件：" + geom + "\n" +
							"返回字段：" + token.fields.toString()
						);
					}
				}
			}
			
			
			/**
			 * 收集并整理查询结果数据 
			 */
			private function createSearchResults(featureSet:FeatureSet, token:Object):void
			{
				var layerName:String = token.label;
				var titleField:String = token.titlefield;
				
				for each( var graphic:Graphic in featureSet.features )
				{
					var attribute:Object = graphic.attributes;
					
					var searchRest:InfoResult = new InfoResult();
					searchRest.title = attribute[titleField];
					searchRest.layerName = layerName;
					searchRest.geometry = graphic.geometry;
					searchRest.primaryCode = configXML.pointRedPic;
					
					searchResultAC.addItem(searchRest);
				}
			}
			
			
			/**
			 * 查询完毕 
			 */
			private function onSearchEnd():void
			{
				CursorManager.removeBusyCursor();
				
				//是否查询完毕改为是
				isSearchEnd = true;
				
				//显示查询结果信息
				arrangeResultInfo();
				
				//将查询结果赋值给翻页控件
				pageNavi.pageData = searchResultAC;
			}
			
			
			/**
			 * 监听清空地图事件 
			 */
			private function onClearMap(event:AppEvent):void
			{
				searchState();
				
				clear();
				
				searchResultItem.dataProvider = null;
				pageNavi.pageData = null;
				txtMessage.text = "";
			}
			
			
			/**
			 * 显示查询结果信息
			 */
			private function arrangeResultInfo():void
			{
				if (searchResultAC.length < 1)
				{
					showMessage("没有搜索到相关信息", false);
					
					searchState();
				}
				else
				{
					clearMessage();
					
					resultState();
				}
			}
			
			
			/**
			 * 改变视图状态为查询 
			 */
			private function searchState():void
			{
				this.currentState = "search";
			}
			
			
			/**
			 * 改变视图状态为结果
			 */
			private function resultState():void
			{
				this.currentState = "result";
			}
			
			
			/**
			 * 监听翻页事件
			 * 创建混合符号
			 */
			private function pageNaviChange(event:UE_PageNavigationEvent):void
			{
				clear(false);
				
				var pageData:ArrayCollection = event.data as ArrayCollection;
				
				var infoAC:ArrayCollection = new ArrayCollection();
				var graphicAC:ArrayCollection = new ArrayCollection();
				
				for(var i:int=0; i<pageData.length; i++)
				{
					//创建混合符号
					var comSymbol:CompositeSymbol = createPointSymbol((i+1).toString());
					
					/*
					向地图上添加信息  
					*/
					var searchInfo:InfoResult = pageData[i] as InfoResult;
					searchInfo.symbol = comSymbol;
					
					graphicAC.addItem(searchInfo.geometry);
					searchInfo.graphic = addPointOnMap(searchInfo);
					infoAC.addItem(searchInfo);
				}
				
				searchResultItem.dataProvider = infoAC;
				
				//MapUni.dataExtent(graphicAC, map);
			}
			
			
			/**
			 * 在地图上添加点 
			 */
			private function addPointOnMap(searchInfo:InfoResult):Graphic
			{
				var mapPoint:MapPoint = new MapPoint();
				
				var pointGra:Graphic = new Graphic();
				
				switch(searchInfo.geometry.type)
				{
					case Geometry.MAPPOINT:
						
						mapPoint = searchInfo.geometry as MapPoint;
						break;
					
					case Geometry.POLYLINE:
						
						var polyLine:Polyline = searchInfo.geometry as Polyline;
						var pointArray:Array = polyLine.paths[0];
						mapPoint = pointArray[Math.round(pointArray.length/2)];
						
						var LineGra:Graphic = new Graphic();
						LineGra.geometry = polyLine;
						LineGra.symbol = new SimpleLineSymbol("solid", 0x0000ff, 0.5, 3);
						graphicLayer.add(LineGra);
						
						break;
					
					case Geometry.POLYGON:
						
						var polygon:Polygon = searchInfo.geometry as Polygon;
						mapPoint = polygon.extent.center;
						
						var geomServerUrl:String = configData.webService.geometryService;
						
						MapUni.relation(geomServerUrl, [mapPoint], [polygon], onRelatrionResult, [polygon, pointGra],null,null);
						
						var PolygonGra:Graphic = new Graphic();
						PolygonGra.geometry = polygon;
						PolygonGra.symbol = new SimpleFillSymbol("solid", 0x0000ff, 0.3, new SimpleLineSymbol());
						graphicLayer.add(PolygonGra);
						break;
				}
				
				pointGra.geometry = mapPoint;
				pointGra.symbol = searchInfo.symbol;
				pointGra.attributes = searchInfo;
				pointGra.toolTip = searchInfo.title;
				
				pointGra.addEventListener(MouseEvent.CLICK, onPointMouseClick);
				pointGra.addEventListener(MouseEvent.ROLL_OVER, onPointMouseOver);
				pointGra.addEventListener(MouseEvent.ROLL_OUT, onPointMouseOut);
				
				labelGraLayer.add(pointGra);
				
				return pointGra;
			}
			
			
			private function onRelatrionResult(resultArray:Array, pointGraArray:Array):void
			{
				if(!resultArray.length)
				{
					var polygon:Polygon = pointGraArray[0];
					var pointGra:Graphic =  pointGraArray[1];
					
					pointGra.geometry = polygon.rings[0][0];
				}
			}
			
			
			/**
			 * 监听气泡窗口的关闭
			 */
			private function onInfoWindowclese(event:Event):void
			{
				var popupRenderer:PopUpRenderer = InfoWindow(event.currentTarget).content as PopUpRenderer;
				if(popupRenderer)
				{
					var graphic:Graphic = popupRenderer.graphic;
					
					graphic.addEventListener(MouseEvent.ROLL_OVER, onPointMouseOver);
					graphic.addEventListener(MouseEvent.ROLL_OUT, onPointMouseOut);
					
					var symbolText:String = getSymbolText(graphic.symbol as CompositeSymbol);
					var comSymbol:CompositeSymbol = createPointSymbol(symbolText);
					graphic.symbol = comSymbol;
				}
			}
			
			
			/**
			 * 监听鼠标点击查询结果图标
			 */
			private function onPointMouseClick(event:MouseEvent):void
			{
				var searchInfo:InfoResult = Graphic(event.currentTarget).attributes as InfoResult;
				
				var graphic:Graphic = searchInfo.graphic;
				var symbolText:String = getSymbolText(graphic.symbol as CompositeSymbol);
				var comSymbol:CompositeSymbol = createSeletedSymbol(symbolText);
				
				graphic.removeEventListener(MouseEvent.ROLL_OVER, removeEvent);
				graphic.removeEventListener(MouseEvent.ROLL_OUT, removeEvent);
				
				graphic.symbol = comSymbol;
				
				MapUni.popupInfoWindow(map, searchInfo.graphic, searchInfo.title, searchInfo.content);
				
				
				function removeEvent():void
				{
					var a:*;
				}
			}
			
			
			/**
			 * 监听鼠标移入查询结果 
			 */
			private function onPointMouseOver(event:MouseEvent):void
			{
				var graphic:Graphic = event.currentTarget as Graphic;
				
				var symbolText:String = getSymbolText(graphic.symbol as CompositeSymbol);
				var comSymbol:CompositeSymbol = createSeletedSymbol(symbolText);
				graphic.symbol = comSymbol;
				
				replaceItemSymbol(symbolText, true);
				
				hightLightGeomtry(graphic.attributes.geometry);
			}
			
			
			/**
			 * 监听鼠标移出查询结果 
			 */
			private function onPointMouseOut(event:MouseEvent):void
			{
				var graphic:Graphic = event.currentTarget as Graphic;
				
				var symbolText:String = getSymbolText(graphic.symbol as CompositeSymbol);
				var comSymbol:CompositeSymbol = createPointSymbol(symbolText);
				graphic.symbol = comSymbol;
				
				replaceItemSymbol(symbolText, false);
				
				baseGraLayer.remove(currGraphic);
			}
			
			
			/**
			 * 替换结果列表中的指定项的符号
			 * 
			 * symbolText:地图上点符号中的数字信息
			 * itemStateType：设置对应地图列表中项的选中状态
			 */
			private function replaceItemSymbol(symbolText:String, itemSelected:Boolean):void
			{
				try
				{
					var itemIndex:uint = Number(symbolText);
					
					var itemRenderer:BufferResultItemRenderer = searchResultItem.getVirtualElementAt(itemIndex-1) as BufferResultItemRenderer;
					
					if(itemRenderer)
					{
						if(itemSelected)
						{
							itemRenderer.currentState = "hovered";
						}
						else
						{
							itemRenderer.currentState = "normal";
						}
					}
				}
				catch(e:Error)
				{
					
				}
			}
			
			
			/**
			 * 获取到图形符号中的文本信息 
			 */
			private function getSymbolText(comSymbol:CompositeSymbol):String
			{
				var symbolText:String = "";
				
				if(comSymbol)
				{
					var symbolAC:ArrayCollection = comSymbol.symbols as ArrayCollection;
					
					for(var i:int=0; i<symbolAC.length; i++)
					{
						if(symbolAC[i] is TextSymbol)
						{
							var textSymbol:TextSymbol = symbolAC[i] as TextSymbol;
							symbolText = textSymbol.text;
						}
					}
				}
				
				return symbolText;
			}
			
			
			/**
			 * 创建普通符号 
			 */
			private function createPointSymbol(text:String):CompositeSymbol
			{
				var picSource:String = configXML.pointSmallPic;
				
				var comSymbol:CompositeSymbol = createComSymbol(picSource, 14, text, 19, 0xffffff);
				
				return comSymbol;
			}
			
			
			/**
			 * 创建选中符号 
			 */
			private function createSeletedSymbol(text:String):CompositeSymbol
			{
				var picUrl:String = configXML.pointBigPic;
				
				var comSymbol:CompositeSymbol = createComSymbol(picUrl, 17, text, 24, 0xffffff);
				
				return comSymbol;
			}
			
			
			/**
			 * 创建混合符号 
			 */
			private function createComSymbol(picUrl:String, picYoffset:uint, textStr:String, textYoffset:uint, textColor:Number):CompositeSymbol
			{
				var picSymbol:PictureMarkerSymbol = new PictureMarkerSymbol(picUrl);
				picSymbol.yoffset = picYoffset;
				
				var textSymbol:TextSymbol = new TextSymbol(textStr);
				textSymbol.yoffset = textYoffset;
				textSymbol.textFormat = new TextFormat();
				textSymbol.textFormat.color = textColor;
				
				var SymbolAC:ArrayCollection = new ArrayCollection([picSymbol, textSymbol]);
				var comSymbol:CompositeSymbol = new CompositeSymbol(SymbolAC);
				
				return comSymbol;
			}
			
			
			/**
			 * 清空 
			 */
			private function clear(isClearGeomLayer:Boolean=true):void
			{
				if(isClearGeomLayer)
				{
					baseGraLayer.clear();
				}
				graphicLayer.clear();
				labelGraLayer.clear();
				map.infoWindow.hide();
			}
			
			
			/**
			 * 显示搜索信息 
			 */
			private function showMessage(msg:String, swfVisible:Boolean):void
			{
				txtMessage.text = msg;
				swfMessage.visible = swfVisible;
				msgVisible = true;
			}
			
			
			/**
			 * 清空搜索信息 
			 */
			private function clearMessage():void
			{
				msgVisible = false;
			}
			
			
			/**
			 * 显示错误信息 
			 */
			private function showErrorInfo(errorString:String):void
			{
				var widgetName:String = configXML.widgetInfo.@widgetName;
				var widgetUsefulness:String = configXML.widgetInfo.@widgetUsefulness;
				
				var classInfo:String = "名称：" + widgetName + "\n" + "功能：" + widgetUsefulness + "\n"; 
				
				MapUni.errorWindow(classInfo + errorString);
			}
			
			
			/**
			 * 监听鼠标移入查询列表 
			 */
			private function mouseOverSearchResult(event:Event):void
			{
				var searchInfo:InfoResult = BufferResultItemRenderer(event.target).data as InfoResult;
				
				var symbolText:String = getSymbolText(searchInfo.symbol as CompositeSymbol);
				var comSymbol:CompositeSymbol = createSeletedSymbol(symbolText);
				
				replaceMapPointSymbol(searchInfo.graphic, comSymbol);
				
				hightLightGeomtry(searchInfo.geometry);
			}
			
			
			
			private function hightLightGeomtry(geom:Geometry):void
			{
				if(geom.type == Geometry.POLYLINE)
				{
					var lineSym:SimpleLineSymbol = new SimpleLineSymbol("solid", 0x0000ff, 1, 3);
					
					currGraphic.geometry = geom;
					currGraphic.symbol = lineSym;
					
					baseGraLayer.add(currGraphic);
				}
				else if(geom.type == Geometry.POLYGON)
				{
					var polygonSym:SimpleFillSymbol = new SimpleFillSymbol("solid", 0x0000ff, 0.8, new SimpleLineSymbol("solid", 0xffffff));
					
					currGraphic.geometry = geom;
					currGraphic.symbol = polygonSym;
					
					baseGraLayer.add(currGraphic);
				}
			}
			
			
			/** 
			 * 监听鼠标移出查询列表
			 */
			private function mouseOutSearchResult(event:Event):void
			{
				var searchInfo:InfoResult = BufferResultItemRenderer(event.target).data as InfoResult;
				
				var symbolText:String = getSymbolText(searchInfo.symbol as CompositeSymbol);
				var comSymbol:CompositeSymbol = createPointSymbol(symbolText);
				
				replaceMapPointSymbol(searchInfo.graphic, comSymbol);
				
				baseGraLayer.remove(currGraphic);
			}
			
			
			/**
			 * 替换地图上某一点位符号 
			 */
			private function replaceMapPointSymbol(graphic:Graphic, symbol:Symbol):void
			{
				var graphicAC:ArrayCollection = labelGraLayer.graphicProvider as ArrayCollection;
				
				for(var i:int=0; i<graphicAC.length; i++)
				{
					var mapGra:Graphic = graphicAC[i] as Graphic;
					
					if(mapGra == graphic)
					{
						graphic.symbol = symbol;
					}
				}
			}
			
			
			/**
			 * 监听结果列表的鼠标点击事件 
			 */
			private function clickSearchResult(event:Event):void
			{
				var searchInfo:InfoResult = BufferResultItemRenderer(event.target).data as InfoResult;
				
				MapUni.dataExtent([searchInfo.geometry], map);
				
				MapUni.popupInfoWindow(map, searchInfo.graphic, searchInfo.title, searchInfo.content);
			}
			
			private function widgetOpenedHandler(event:Event):void
			{
				wTemplate.enableResizeButton = false;
			}
			
			private function widgetClosedHandler(event:Event):void
			{
				CursorManager.removeBusyCursor();
				
				onClearMap(null);
			}
			
			
		]]>
	</fx:Script>
	
	
	<viewer:states>
		<s:State name="search"/>
		<s:State name="result"/>
	</viewer:states>
	
	<viewer:transitions>
		<s:Transition autoReverse="true" fromState="*" toState="*">
			<s:Sequence>
				<s:Parallel>
					<s:Resize duration="500" target="{wTemplate}" />
				</s:Parallel>
				<s:Parallel>
					<s:Fade duration="500" target="{resultsList}"  />
				</s:Parallel>
			</s:Sequence>
		</s:Transition>
	</viewer:transitions>
	
	
	<viewer:WidgetTemplate id="wTemplate"
						   minWidth="330"
						   width="340" 
						   width.result="380"
						   height.search="130" 
						   height.result="450" 
						   closed="widgetClosedHandler(event)"
						   open="widgetOpenedHandler(event)"
						   >
		<s:VGroup width="100%" height="100%"
				  paddingTop="5"
				  gap="4"
				  >
			<s:HGroup width="100%"
					  gap="5"
					  horizontalAlign="center">
				<mx:Image click="{activateSearchTool(DrawTool.MAPPOINT)}" source="assets/images/i_draw_point.png" useHandCursor="true" buttonMode="true" toolTip="点查询" width="40" height="40"/>
				<mx:Image click="{activateSearchTool(DrawTool.POLYLINE)}" source="assets/images/i_draw_line.png" useHandCursor="true" buttonMode="true" toolTip="线查询" width="40" height="40"/>
				<mx:Image click="{activateSearchTool(DrawTool.POLYGON)}" source="assets/images/i_draw_poly.png" useHandCursor="true" buttonMode="true" toolTip="多边形查询" width="40" height="40"/>
			</s:HGroup>
			<mx:HBox horizontalAlign="center" width="100%" horizontalGap="10">
				<mx:Label text="缓冲半径：" styleName="WidgetText"/>	
				<s:NumericStepper id="bufferRadiusInput"  value="1000" stepSize="500" minimum="500" maximum="100000"  />
				<mx:Label text="米" styleName="WidgetText" />			
			</mx:HBox>
			
			<!-- tooltip -->
			<s:HGroup id="boxMessage" width="100%" verticalAlign="bottom"
					  includeInLayout="{msgVisible}" visible="{msgVisible}"  >
				<!--<mx:Spacer width="8" />-->
				<mx:Image id="swfMessage" source="assets/images/loader.swf" visible="false"/>
				<s:Label id="txtMessage" width="98%"  verticalAlign="middle"/>
			</s:HGroup>
			
			<mx:Spacer height="5" />
			
			<!-- result-->
			<s:Group id="resultsList"
					 width="100%" height="100%" visible.search="false" visible.result="true"
					 >
				<s:layout>
					<s:VerticalLayout gap="3"/>
				</s:layout>
				
				<s:Scroller width="360" height="100%" >
					<Buffer:BufferResultDataGroup id="searchResultItem" 
												  searchResultClick="clickSearchResult(event)"
												  searchResultMouseOut="mouseOutSearchResult(event)"
												  searchResultMouseOver="mouseOverSearchResult(event)">
						<Buffer:layout>
							<s:VerticalLayout gap="2"
											  horizontalAlign="justify"
											  useVirtualLayout="true"/>
						</Buffer:layout>
					</Buffer:BufferResultDataGroup>
				</s:Scroller>
				<pageNavigation:UC_PageNavigation id="pageNavi" 
												  pageSize="25"
												  pageChange="pageNaviChange(event)" 
												  />
			</s:Group>
		</s:VGroup>
	</viewer:WidgetTemplate>
	
</viewer:BaseWidget>
